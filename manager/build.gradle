apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion rootProject.ext.targetSdkVersion
    ndkVersion rootProject.ext.ndkVersion
    defaultConfig {
        applicationId "moe.shizuku.privileged.api"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs 'en', 'ar', 'de', 'es-rCL', 'ja', 'pt-rBR', 'ru', 'tr', 'uk', 'zh-rCN', 'zh-rTW'
        resValue "string", "versionName", "${rootProject.ext.versionName}"
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=c++_static'
            }
        }
    }
    buildFeatures {
        viewBinding = true
    }
    signingConfigs {
        sign
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.sign
        }
        release {
            signingConfig signingConfigs.sign
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path 'src/main/jni/CMakeLists.txt'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    packagingOptions {
        exclude '/META-INF/*.version'
        exclude '/META-INF/*.kotlin_module'
        exclude '/kotlin/**'
    }
    lintOptions {
        checkReleaseBuilds false
    }
    dependenciesInfo {
        includeInApk false
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "shizuku-${variant.versionName}-${variant.name}.apk"

        variant.assembleProvider.get().doLast {
            def outDir = new File(rootDir, "out")
            def mappingDir = new File(outDir, "mapping").absolutePath
            def apkDir = new File(outDir, "apk").absolutePath

            if (variant.getBuildType().isMinifyEnabled()) {
                copy {
                    from variant.mappingFileProvider.get()
                    into mappingDir
                    rename { String fileName -> "mapping-${variant.versionName}.txt" }
                }
                copy {
                    from outputFile
                    into apkDir
                }
            }
        }
    }
}

configurations.all {
    exclude group: 'androidx.appcompat', module: 'appcompat'
    exclude group: 'moe.shizuku.preference', module: 'preference-android'
    exclude group: 'moe.shizuku.preference', module: 'preference-simplemenu-android'
}

repositories {
    //mavenLocal()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.7"

    //compileOnly project(':hidden-api-common')
    implementation project(':hidden-api-common-bridge')
    implementation project(':api')
    implementation project(':shared')

    implementation 'androidx.browser:browser:1.2.0'
    implementation "androidx.core:core-ktx:1.3.0"
    implementation "androidx.fragment:fragment-ktx:1.2.5"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.localbroadcastmanager:localbroadcastmanager:1.0.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"

    implementation 'com.github.topjohnwu.libsu:core:2.5.1'

    def preferenceLibraryVersion = '4.2.0-alpha02'
    implementation "moe.shizuku.preference:preference-appcompat:$preferenceLibraryVersion"
    implementation "moe.shizuku.preference:preference-simplemenu-appcompat:$preferenceLibraryVersion"

    implementation "rikka.annotation:annotation:1.0.0"
    implementation "rikka.appcompat:appcompat:1.2.0-rc01"
    implementation "rikka.core:core:1.2.1"
    implementation "rikka.material:material:1.4.1"
    implementation "rikka.html:html:1.0.0"
    implementation "rikka.recyclerview:recyclerview-utils:1.1.0"

    implementation 'moe.shizuku.fontprovider:api:10'

    implementation 'me.weishu:free_reflection:2.2.0' // bypass hidden api restriction

    implementation 'org.bouncycastle:bcpkix-jdk15on:1.65'

    implementation 'me.zhanghai.android.appiconloader:appiconloader:1.2.0'
}

apply from: rootProject.file('signing.gradle')