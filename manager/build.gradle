import java.nio.file.Files
import java.nio.file.Paths
import java.util.stream.Collectors

plugins {
    id('com.android.application')
    id('kotlin-android')
    id('dev.rikka.tools.refine.gradle-plugin')
}

android {
    compileSdkVersion rootProject.ext.targetSdkVersion
    defaultConfig {
        applicationId "moe.shizuku.privileged.api"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=none'
            }
        }
    }
    buildFeatures {
        viewBinding true
        prefab true
    }
    signingConfigs {
        sign
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.sign
        }
        release {
            signingConfig signingConfigs.sign
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path 'src/main/jni/CMakeLists.txt'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
        useIR = true
    }
    packagingOptions {
        exclude '/META-INF/*.version'
        exclude '/META-INF/*.kotlin_module'
        exclude '/kotlin/**'
    }
    lintOptions {
        checkReleaseBuilds false
    }
    dependenciesInfo {
        includeInApk false
    }
}

def optimizeReleaseResources = task('optimizeReleaseResources').doLast {
    def aapt2 = Paths.get(project.android.sdkDirectory.path, 'build-tools', project.android.buildToolsVersion, 'aapt2')
    def zip = Paths.get(project.buildDir.path, 'intermediates',
            'processed_res', 'release', 'out', "resources-release.ap_")
    def optimized = new File("${zip}.opt")
    def cmd = exec {
        commandLine aapt2, 'optimize', '--collapse-resource-names',
                '--shorten-resource-paths',
                '--resources-config-path', 'aapt2-resources.cfg',
                '-o', optimized, zip
        ignoreExitValue false
    }
    if (cmd.exitValue == 0) {
        delete(zip)
        optimized.renameTo("$zip")
    }
}

afterEvaluate {
    tasks.getByName('processReleaseResources').finalizedBy(optimizeReleaseResources)
    tasks.getByName('preReleaseBuild').dependsOn(':shell:assembleRelease')
    tasks.getByName('preDebugBuild').dependsOn(':shell:assembleDebug')

    def stream = Files.list(Paths.get(projectDir.getAbsolutePath(), "src/main/res"))
    def rawLocales = stream
            .filter(file -> Files.isDirectory(file)
                    && file.toFile().getName().startsWith("values-")
                    && Files.exists(file.resolve("strings.xml"))
                    && Files.size(file.resolve("strings.xml")) > 62)
            .map(java.nio.file.Path::getFileName)
            .map(path -> path.toFile().getName().substring("values-".length()))
            .collect(Collectors.toList())
    stream.close()

    rawLocales.add('en')

    def locales = rawLocales.stream()
            .map(name -> name.split("-", 2))
            .map { String[] names ->
                if (names.length == 1 && names[0] == "b+es+419") {
                    return new Locale("es", "419", "")
                } else {
                    if (names.length == 1)
                        return new Locale(names[0])
                    else
                        return new Locale(names[0], names[1].substring("r".length()))
                }
            }
            .map { locale -> locale.toLanguageTag() }
            .collect(Collectors.toList())
    stream.close()

    locales.sort(String.CASE_INSENSITIVE_ORDER)

    def displayLocaleMapping = [
            "zh-CN": "zh-Hans",
            "zh-TW": "zh-Hant",
    ]

    def displayLocales = locales.stream()
            .map(s -> displayLocaleMapping.get(s) ?: s)
            .collect(Collectors.toList())

    println "Locales $locales"
    println "Display locales $displayLocales"

    def localesString = String.join(",", locales.stream().map(s -> "\"$s\"").collect(Collectors.toList()))
    def displayLocalesString = String.join(",", displayLocales.stream().map(s -> "\"$s\"").collect(Collectors.toList()))

    android.applicationVariants.forEach { variant ->
        variant.mergedFlavor.setResourceConfigurations(rawLocales)

        def variantNameCapitalized = variant.name.capitalize()
        def outSrcDir = file("$buildDir/generated/source/locales/${variant.name}")
        def outSrc = file("$outSrcDir/rikka/shizuku/manager/ShizukuLocales.java")
        def genLocales = tasks.register("generate${variantNameCapitalized}LocalesSource") {
            inputs.files("src/main/res")
            outputs.file(outSrc)
            doLast {
                def outSteam = new PrintStream(outSrc)
                outSteam.print(
                        """package rikka.shizuku.manager;
                                  |
                                  |public final class ShizukuLocales {
                                  |    public static final String[] LOCALES = {"SYSTEM",$localesString};
                                  |    public static final String[] DISPLAY_LOCALES = {"SYSTEM",$displayLocalesString};
                                  |}""".stripMargin())
                outSteam.flush()
                outSteam.close()
            }
        }
        variant.registerJavaGeneratingTask(genLocales, outSrcDir)

        def kotlinCompileTask = (SourceTask) tasks.findByName("compile${variantNameCapitalized}Kotlin")
        if (kotlinCompileTask != null) {
            kotlinCompileTask.dependsOn(genLocales)
            kotlinCompileTask.source(outSrcDir)
        }
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "shizuku-v${variant.versionName}-${variant.name}.apk"

        variant.assembleProvider.get().doLast {
            def outDir = new File(rootDir, "out")
            def mappingDir = new File(outDir, "mapping").absolutePath
            def apkDir = new File(outDir, "apk").absolutePath

            if (variant.getBuildType().isMinifyEnabled()) {
                copy {
                    from variant.mappingFileProvider.get()
                    into mappingDir
                    rename { String fileName -> "mapping-${variant.versionName}.txt" }
                }
                copy {
                    from outputFile
                    into apkDir
                }
            }
        }
    }
}

configurations.all {
    exclude group: 'androidx.appcompat', module: 'appcompat'
}

repositories {
    maven {
        url 'https://jitpack.io'
        content {
            includeGroup('com.github.topjohnwu.libsu')
        }
    }
    mavenLocal()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2'

    implementation project(':server')
    implementation project(':rish')
    implementation project(':starter')
    implementation project(':api')
    implementation project(':provider')
    compileOnly 'dev.rikka.hidden:stub:1.0.0'

    implementation 'androidx.browser:browser:1.4.0'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.fragment:fragment-ktx:1.4.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'

    implementation 'com.google.android.material:material:1.6.0-alpha02'

    implementation 'com.github.topjohnwu.libsu:core:3.2.1'

    implementation 'dev.rikka.rikkax.appcompat:appcompat:1.4.1'
    implementation 'dev.rikka.rikkax.compatibility:compatibility:2.0.0'
    implementation 'dev.rikka.rikkax.core:core-ktx:1.3.4'
    implementation 'dev.rikka.rikkax.material:material:2.1.0'
    implementation 'dev.rikka.rikkax.material:material-preference:1.0.0'
    implementation 'dev.rikka.rikkax.html:html-ktx:1.1.2'
    implementation 'dev.rikka.rikkax.recyclerview:recyclerview-adapter:1.3.0'
    implementation 'dev.rikka.rikkax.recyclerview:recyclerview-ktx:1.3.1'
    implementation 'dev.rikka.rikkax.insets:insets:1.2.0'
    implementation 'dev.rikka.rikkax.layoutinflater:layoutinflater:1.2.0'
    implementation 'dev.rikka.rikkax.widget:borderview:1.1.0'
    implementation 'dev.rikka.rikkax.preference:simplemenu-preference:1.0.3'
    implementation 'dev.rikka.rikkax.lifecycle:lifecycle-resource-livedata:1.0.0'
    implementation 'dev.rikka.rikkax.lifecycle:lifecycle-shared-viewmodel:1.0.0'
    implementation 'dev.rikka.rikkax.lifecycle:lifecycle-viewmodel-lazy:1.0.0'

    implementation 'io.github.vvb2060.ndk:boringssl:2.1'
    implementation 'dev.rikka.ndk.thirdparty:cxx:1.2.0'

    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:4.3'

    implementation 'org.bouncycastle:bcpkix-jdk15on:1.70'

    implementation 'me.zhanghai.android.appiconloader:appiconloader:1.3.1'
}

apply from: rootProject.file('signing.gradle')
