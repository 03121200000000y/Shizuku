apply plugin: 'idea'

idea.module {
    excludeDirs += file('out')
}

buildscript {
    apply from: 'api/manifest.gradle'

    ext.kotlinVersion = '1.5.21'
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.15.1'
        classpath 'dev.rikka.tools:hidden-api-refine:1.1.0'
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
    tasks.withType(Javadoc) {
        failOnError false
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def gitCommitId = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitCommitCount = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())
def baseVersionName = "${api_version_major}.2.0"
ext {
    minSdkVersion = 23
    targetSdkVersion = 30
    versionCode = gitCommitCount
    versionName = "${baseVersionName}.r${gitCommitCount}.${gitCommitId}"
}
