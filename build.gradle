buildscript {
    apply from: 'api/manifest.gradle'
    ext.kotlinVersion = '1.8.0'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.20.0'
    }
}

plugins {
    id 'com.android.application' version '7.4.2' apply false
    id 'com.android.library' version '7.4.2' apply false
    id 'org.jetbrains.kotlin.jvm' version '1.8.0' apply false
    id 'dev.rikka.tools.autoresconfig' version '1.2.1' apply false
    id 'dev.rikka.tools.materialthemebuilder' version '1.3.3' apply false
    alias libs.plugins.refine apply false
}

apply plugin: 'idea'

idea.module {
    excludeDirs += file('out')
}

allprojects {
    tasks.withType(Javadoc) {
        failOnError false
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

subprojects {
    plugins.withId("com.android.base") {
        plugins.apply('dev.rikka.tools.refine')

        android {
            compileSdk = 33
            defaultConfig {
                minSdk = 23
                targetSdk = 32
            }
            compileOptions {
                sourceCompatibility = JavaVersion.VERSION_1_8
                targetCompatibility = JavaVersion.VERSION_1_8
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def gitCommitId = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitCommitCount = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())
def baseVersionName = "${api_version_major}.3.0"
ext {
    minSdkVersion = 23
    targetSdkVersion = 32
    versionCode = gitCommitCount
    versionName = "${baseVersionName}.r${gitCommitCount}.${gitCommitId}"
}
