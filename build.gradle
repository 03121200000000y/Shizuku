apply plugin: 'idea'

idea.module {
    excludeDirs += file('out')
    resourceDirs += file('magisk-shizuku-starter')
}

buildscript {
    ext.kotlinVersion = '1.4.21'
    repositories {
        jcenter()
        google()
        maven { url 'http://storage.googleapis.com/r8-releases/raw' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'

        // for org.bouncycastle:bcprov-jdk15on (https://issuetracker.google.com/issues/159151549)
        classpath 'org.ow2.asm:asm:8.0.1'
        classpath 'org.ow2.asm:asm-util:8.0.1'
        classpath 'org.ow2.asm:asm-commons:8.0.1'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
    tasks.withType(Javadoc) {
        failOnError false
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def gitCommitId = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitCommitCount = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())
def baseVersionName = '5.1.0'

ext {
    minSdkVersion = 23
    targetSdkVersion = 30
    ndkVersion = "21.3.6528147"
    versionCode = gitCommitCount
    versionName = "${baseVersionName}.r${gitCommitCount}.${gitCommitId}"

    apiVersionName = '5.0.0-alpha03'
}